#!/bin/fish

set AGS_CONFIG_DIR $XDG_CONFIG_HOME/ags
set SCSS_PATH $AGS_CONFIG_DIR/style/main.scss
set CSS_PATH $AGS_CONFIG_DIR/dist/main.css
set TF %H%M%S

function restart-ags
    systemctl restart --user ags.service
end

function apply-css
    sass $SCSS_PATH $CSS_PATH
    ags --run-js "const App = (await import('./app.js')).default; App.resetCss(); App.applyCss(\"$CSS_PATH\");"
end

inotifywait --quiet --monitor --recursive $AGS_CONFIG_DIR --event create,modify,delete --format '%w %f %e %T' --timefmt $TF |
    while read DIRECTORY FILE EVENT TIME
        set delta (math (date +$TF) - $TIME)
        if test $delta -lt 2 -a $delta -gt -2
            switch (path extension $FILE)
                case .js
                    restart-ags
                case .scss
                    apply-css
            end
            sleep 2
        end
    end


